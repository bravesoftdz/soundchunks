; Z80 driver for replaying RSE SoundChunks PCM streams
; By GliGli

; ###########################	   define      ##############################

	INCLUDE "z80_def.i80"	; basic definitions

; ###########################	    var	       ##############################

RSCPARAMS	EQU	0x100

RSCFLAGS	EQU	RSCPARAMS+0x00	// 0: 68k ready; 1: Z80 ready; 2: track finished; 3: stop requested
RSCBANK		EQU	RSCPARAMS+0x01
RSCBANKPOS	EQU	RSCPARAMS+0x02  // 15: "68k bus available" flag, managed by the 68k
RSCFRAMELEFT	EQU	RSCPARAMS+0x04

SAMPLES_PER_CHUNK_SHIFT		EQU	2
SAMPLES_PER_CHUNK		EQU	(1 << SAMPLES_PER_CHUNK_SHIFT)
CHUNKS_PER_FRAME		EQU	256

CHUNKBUFFER0	EQU	0x0400
INDEXBUFFER0	EQU	0x0800
CHUNKBUFFER1	EQU	0x1400
INDEXBUFFER1	EQU	0x1800

CHUNKBUFFER_SZ	EQU	(CHUNKS_PER_FRAME * SAMPLES_PER_CHUNK)
INDEXBUFFER_SZ	EQU	256 * 9

; ###########################	   macro       ##############################

	INCLUDE "z80_mac.i80"  ; basic macros

	; data -> YM2612 low reg address
	macro	ymW0 addr, data
	
	LD	HL, YMPORT0

	LD	(HL), addr
	INC	L
	LD	(HL), data
	DEC	L

.wait:	BIT	7, (HL)
	JP	NZ, .wait

	endm
	
	; data -> YM2612 high reg address
	macro	ymW1 addr, data
	
	LD	HL, YMPORT2

	LD	(HL), addr
	INC	L
	LD	(HL), data
	DEC	L

.wait:	BIT	7, (HL)
	JP	NZ, .wait

	endm
	
	; 8bit sample in smp
	macro	ymWSH smp
	
	LD	(HL), 0x2a
	INC	L
	LD	(HL), smp
	DEC	L

	endm

	; 9th sample bit in Carry
	macro	ymWSL
	
	LD	(HL), 0x2c
	INC	L

	SBC	A, A
	AND	L		; L = 9 here, so it's fine to write the 9th DAC bit (which is bit 3; bit 0 is ignored)

	LD	(HL), A
	DEC	L

	endm

	macro	ymWSL1
	
	LD	(HL), 0x2c
	INC	L
	
	LD	(HL), L		; L = 9 here, so it's fine to write the 9th DAC bit (which is bit 3; bit 0 is ignored)
	DEC	L

	endm

	; wait YM2612 timer A and reset it
	macro	ymWT

.wait:	BIT	0, (HL)
	JR	Z, .wait

	LD	(HL), 0x27
	INC	L

	LD	(HL), 0x15
	DEC	L
	
	endm

	; copy stream data to the next buffer when the 68k bus is available
	macro	preloadBuffer
	
	LD	HL, (RSCBANKPOS)

	LD	A, H
	AND	IXH
	JP	P, .busy_or_buffer_full

	LDI
	LDI
	LDI

	LD	(RSCBANKPOS), HL

	INC	IX

.busy_or_buffer_full:

	LD	HL, YMPORT0 + 8		; we need L = 8 for the algo and it points on a mirror
		
	endm
	
	; run per sample background tasks
	macro	sampleYield idx

	if	idx == 0
		; nothing (chunk change will take a bit of time already)
	else
	if	idx == 1
		ymWT
	else
	if	idx == 2
		preloadBuffer
	else
	; if	idx == 3
		ymWT
	endif
	endif
	endif

	endm

	; play a chunk sample in 8bit mode
	macro	playHiSample smp, idx
	
	ymWSL1
	ymWSH smp

	sampleYield idx

	endm
	
	; play a chunk sample in 9bit mode
	macro	playLoSample smp, idx

	SRL	smp
	ymWSL
	LD	A, smp
	ADD	H		; H = 64 here
	ymWSH A

	sampleYield idx

	endm

; ###########################	    init       ##############################

	ORG	0x0000

init:
	DI			; disable ints
	JP	start		; jump to start


; ###########################	    main       ##############################

	BLOCK	0x0110-$

start:
	; wait for 68k side init
	LD	A, (RSCFLAGS)
	RRCA
	JP	NC, start

	; set config
	LD	A, (RSCBANK)
	setBankFast

	; Enable DAC
	ymW0	0x2b, 0x80
	
	; DAC Pan = LR
	ymW1	0xb6, 0xc0

	; Timer A enabled @ 26390Hz / 2
	ymW0	0x24, 0xff
	ymW0	0x25, 0x00
	ymW0	0x27, 0x15

	; DAC init sample
	ymW0	0x2a, 0x80
	ymW0	0x2c, 0x08

	; preload buffers
	
	LD	HL, 0x8000
	
	LD	E, (HL)
	INC	L
	LD	D, (HL)
	INC	L
	LD	(RSCFRAMELEFT), DE

	LD	DE, CHUNKBUFFER0
	LD	BC, CHUNKBUFFER_SZ + INDEXBUFFER_SZ
	LDIR

	; init player

	EXX
	LD	HL, YMPORT0 + 8		; we need L = 8 for the algo and it points on a mirror
	EXX

	LD	(RSCBANKPOS), HL
	LD	H, CHUNKBUFFER0 >> 10
	LD	IYH, high INDEXBUFFER1

	; Z80 side init done
	LD	A, (RSCFLAGS)
	OR	2
	LD	(RSCFLAGS), A

restart:	
	; double buffer handling / switch

	LD	A, IYH
	XOR	0x10
	LD	IYH, A

	LD	D, A
	LD	E, 0
	EXX
	XOR	0x10
	LD	D, A
	LD	E, 0
	EXX

	LD	IX, 65536 - INDEXBUFFER_SZ / 3	; buffer preload counter
	LD	IYL, 0				; block counter (256 blocks)

	; handle 68k track stop request

	LD	A, (RSCFLAGS)
	AND	8
	JP	Z, main_loop

	LD	A, (RSCFLAGS)
	OR	4
	LD	(RSCFLAGS), A

	ymW0	0x2a, 0x80
	ymW0	0x2c, 0x08

stop_loop:
	JR	stop_loop

chunkIdx_loSmp:
	POP	BC
	playLoSample C, 0
	playLoSample B, 1
	POP	BC
	playLoSample C, 2
	playLoSample B, 3
	EXX
	
	DJNZ	chunkIdx_loop

	; main loop, one 8 chunks block at a time

main_loop:
	
	; check for end of frame

	DEC	IYL
	JP	Z, restart

	; load bit shift for 8 chunk idxs
	LD	A, (DE)
	LD	C, A
	INC	DE

	LD	B, 8

chunkIdx_loop:

	; load chunk index

	LD	A, (DE)
	INC	DE

	; compute chunk address

	LD	L, A
	LD	A, H
	ADD	HL, HL
	ADD	HL, HL

	LD	SP, HL

	LD	H, A

	; play the chunk samples

	SLA	C
	EXX
	JP	NC, chunkIdx_loSmp
	
	POP	BC
	playHiSample C, 0
	playHiSample B, 1
	POP	BC
	playHiSample C, 2
	playHiSample B, 3
	EXX

	DJNZ	chunkIdx_loop
	JP	main_loop

	END
